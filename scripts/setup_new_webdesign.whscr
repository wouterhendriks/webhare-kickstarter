<?wh
LOADLIB "wh::filetypes/csv.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "module::publisher/contenttypes.whlib";
LOADLIB "module::publisher/internal/siteprofiles/reader.whlib";
LOADLIB "module::publisher/siteapi.whlib";
LOADLIB "module::system/database.whlib";
LOADLIB "module::system/whfs.whlib";
LOADLIB "module::system/internal/resources.whlib";
LOADLIB "module::system/internal/devtools/support.whlib";

OBJECT trans := OpenPrimary();
trans->BeginWork();

RECORD args := ParseArguments(GetConsoleArguments(),
                              [[ name := "title", type := "param" ]
                              ,[ name := "name", type := "param" ]
                              ,[ name := "templatetag", type := "param" ]
                              ]);

IF (args.title = "" OR args.name = "" OR args.templatetag = "")
  Abort('Usage: runscript setup_new_webdesign.whscr "Title" "Name" "Template tag"');

STRING template := args.templatetag;
STRING module := args.name;
STRING title := args.title;
STRING name := args.name;

// setup webdesign
RECORD setup := SetupWebDesign(template, module, name
                              ,[ title := title
                               , defaultlanguage := "nl"
                               ]);

// create new site
OBJECT newfolder := OpenWHFSRootObject()->CreateFolder([ name := args.title ]);
OBJECT newsite := CreateSiteFromFolder(newfolder->id);

// update site settings with site design
OBJECT sitesettingstype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/sitesettings");
RECORD sitesettings := sitesettingstype->GetInstanceData(newsite->root);
sitesettingstype->SetInstanceData(newsite->id, [ sitedesign := module || ":" || name ]);

// try to find localhost:8001 webserver so we can assign this site
INTEGER defaultoutputserverid := SELECT AS INTEGER id
                                   FROM system.webservers
                                  WHERE ToUpperCase(baseurl) = 'HTTP://LOCALHOST:8001/';

IF (defaultoutputserverid > 0)
  newsite->SetPrimaryOutput(defaultoutputserverid, "/" || GetSafeFileName(name) || "/");

// create an index file
OBJECT homepage := newsite->rootobject->CreateFile([ name := "index.html"
                                                   , title := "Homepage"
                                                   , type := 5 // HTML file
                                                   , publish := TRUE
                                                   ]);

newsite->rootobject->UpdateMetaData([ indexdoc := homepage->id ]);

RECORD ARRAY errors := trans->CommitWork();

IF (Length(errors) > 0)
  Abort("Errors in setup_new_webdesign.whscr: " || AnyToString(errors,'tree'));

Print("\n## ----------------------- Result ----------------------- ##\n\n"
   || "Title:\n" || title || "\n"
   || "\n"
   || "Module name (converted from title):\n" || name || "\n"
   || "\n"
   || "Assuming a default install, direct URL to the site in the Publisher:\n"
   || "http://localhost:8000/?app=publisher(" || newsite->name || ")\n"
   || "\n"
   || "Output URL:\n"
   || newsite->webroot || "\n"
   || "\n"
   || "You can go to the created module using the following command:\n"
   || "whcd " || name || "\n"
   || "\n"
   || "Or to the newly created webdesign folder:\n"
   || "whcd " || name || "/webdesigns/" || name || "\n");
